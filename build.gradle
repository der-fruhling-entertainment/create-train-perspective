plugins {
    id 'eclipse'
    id 'idea'
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.5-SNAPSHOT" apply false
    id "maven-publish"
}

architectury {
    minecraft = rootProject.minecraft_version
}

group = maven_group
version = "${mod_version}+mc1.18.2"

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "maven-publish"

    configurations {
        mixinDependency
    }

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings loom.officialMojangMappings()
        mixinDependency("net.fabricmc:sponge-mixin:0.12.5+mixin.0.8.5") { transitive = false }
    }

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/der-fruhling-entertainment/create-train-perspective")
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                    password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
                }
            }
        }
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    base {
        archivesName = rootProject.archives_base_name
    }

    version = rootProject.version
    group = rootProject.group

    repositories {
        maven { url = 'https://maven.tterrag.com/' }
        maven { url = "https://mvn.devos.one/snapshots/" }
        maven { url = "https://mvn.devos.one/releases/" }
        maven { url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/" }
        maven { url = "https://maven.jamieswhiteshirt.com/libs-release" }
        maven { url = "https://jitpack.io/" }
        maven { url = "https://api.modrinth.com/maven/" }
        maven { url "https://maven.shedaniel.me/" }
        maven { url "https://maven.terraformersmc.com/releases/" }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }
}
